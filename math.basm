.depth
{
; Y = X or Y; X = depth
    TXA
    ASL  A
    TAX
    LDA  divtable, X
    STA  matha
    LDA  divtable + 1, X
    STA  matha + 1
    TYA
    PHP
    BPL  mulpos
    EOR  #255
    SEC
    ADC  #0
.mulpos
    STA  mathb
    LDY  #0
    STY  mathc + 1
.mulloop
    LSR  mathb
    BCC  mulskip
    CLC
    TYA
    ADC  matha
    TAY
    LDA  mathc + 1
    ADC  matha + 1
    STA  mathc + 1
.mulskip
    ASL  matha
    ROL  matha + 1
    LDA  mathb
    BNE  mulloop

    STY  mathc
    PLP
    BPL  mulpos2
    LDA  #0
    SEC
    SBC  mathc
    STA  mathc
    LDA  #0
    SBC  mathc + 1
    STA  mathc + 1
.mulpos2
    RTS
}

ALIGN    128
.divtable
    FOR  n, 1, 64
    EQUW  36000 / (n + 31)
    NEXT
    