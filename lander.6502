; Main lander code

oswrch      = &ffee
oscli       = &fff7
osbyte      = &fff4
screen      = &6800
screen1     = &5000
screen2     = &6800

displaysize = 7
centresize  = 3
maxypos     = 24 * 8
platheight  = 42
land_offset = 40

DEBUG_VALUES = FALSE

;map      = &1000 ; 64x64 map

    GUARD    screen1
;GUARD    map

    INCLUDE "zeropage.6502"
    INCLUDE "macros.6502"

    ORG &1a00

.start
    LDX #LO(tape)
    LDY #HI(tape)
    JSR oscli
    JSR set_mode
    JSR init_screen
    JSR clear_bank
    JSR swap_banks
    JSR clear_bank
    JSR fixship
    LDA #&54
    STA rand_seed

IF (DEBUG_VALUES)
    LDX #LO(heading_addr)
    LDY #HI(heading_addr)
    LDA #1
    JSR plot_char
    LDA #2
    JSR plot_nextchar
    LDA #3
    JSR plot_nextchar
    LDX #LO(altitude_addr)
    LDY #HI(altitude_addr)
    LDA #4
    JSR plot_char
    LDA #5
    JSR plot_nextchar
    LDA #6
    JSR plot_nextchar

    LDX #LO(direction_addr)
    LDY #HI(direction_addr)
    LDA #13
    JSR plot_char

    LDX #LO(score_addr)
    LDY #HI(score_addr)
    LDA #8
    JSR plot_char

    LDX #LO(fuel_addr)
    LDY #HI(fuel_addr)
    LDA #9
    JSR plot_char
ENDIF

    INCLUDE "gameinit.6502"

.endforever

    INCLUDE "hcalc.6502"

    INCLUDE "landcalc.6502"
    INCLUDE "hud.6502"
    INCLUDE "ground.6502"

IF (DEBUG_VALUES)
    INCLUDE "debug.6502"
ENDIF

    JSR move_particles
    JSR swap_banks
    JSR clear_bank

    LDA #255
    STA pixelmask
    STA pixelmask + 1
    STA lastcol

    INCLUDE "landplot.6502"
    INCLUDE "keyboard.6502"
    INCLUDE "thrustcalc.6502"
    INCLUDE "baseptr.6502"

    JSR plot_ship
    JSR plot_particles

    LDA onplatform
    BMI player_dead
    JMP endforever
.player_dead
    LDA current_screen_hi
    CLC
    ADC #2
    STA jtmp + 1
    LDA #0
    STA jtmp
    LDY #0
    LDX #20
.newseed
    RAND8
    STA mathc
    RAND8
    STA mathc + 1
.fill_screen
    CPX #18
    BCS use_rand
    CPX #3
    BCC use_rand
    CPY #24
    BCC use_rand
    CPY #256 - 24
    BCS use_rand
    LDA #0
    STA (jtmp), Y
    INY
    STA (jtmp), Y
    INY
    STA (jtmp), Y
    INY
    STA (jtmp), Y
    INY
    STA (jtmp), Y
    INY
    STA (jtmp), Y
    INY
    STA (jtmp), Y
    INY
    STA (jtmp), Y
    INY
    BNE fill_screen
    BEQ next_line
.use_rand
    RAND16
    AND #&aa
    STA (jtmp), Y
    INY
    RAND8
    AND #&55
    STA (jtmp), Y
    INY
    BNE fill_screen
.next_line
    INC jtmp + 1
    DEX
    BPL newseed
    LDA #255
    STA pixelmask
    STA pixelmask + 1

    LDA #24
    STA x0
    LDA #40
    STA y0
    STA y1
    LDA #255 - 24
    STA x1
    JSR plot_line

    LDA #24
    STA x0
    LDA #20 * 8
    STA y0
    STA y1
    LDA #255 - 24
    STA x1
    JSR plot_line

    LDA #24
    STA x0
    STA x1
    LDA #40
    STA y0
    LDA #20 * 8
    STA y1
    JSR plot_line

    LDA #255 - 24
    STA x0
    STA x1
    LDA #40
    STA y0
    LDA #20 * 8
    STA y1
    JSR plot_line

    JSR swap_banks
.dead
    JMP dead
    
.xlocs
    SKIP displaysize * displaysize
.ylocs
    SKIP displaysize * displaysize
.collocs
    SKIP displaysize * displaysize
.treeheight
    SKIP displaysize

.ptrmap
    DIRCHARS

.tape
    EQUS "TAPE", 13

    INCLUDE "screen.6502"
    INCLUDE "draw.6502"
    INCLUDE "math.6502"
    INCLUDE "ship.6502"
    INCLUDE "particles.6502"

    ALIGN 256
.map
    INCBIN  "map.bin"
.end
    SKIP 0

    SAVE    "Lander", start, end
