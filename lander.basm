oswrch   = &ffee
screen   = &6800
screen1  = &5000
screen2  = &6800

map      = &4000 ; 64x64 map

GUARD    screen1
GUARD    map

INCLUDE   "zeropage.basm"

ORG &2000

.start
    JSR  set_mode
    JSR  init_bits
    JSR  init_screen
    JSR  clear_bank
    JSR  swap_banks
    JSR  clear_bank
    
    LDX  #4
    JSR  set_colour
    LDA  #0
    STA  xoffset
    STA  yoffset
    
.endforever
    LDA  #0
    STA  offset

{
    LDA  #6
    STA  jtmp
.yloop
    LDA  jtmp
    ASL  A
    ASL  A
    ASL  A
    ADC  yoffset
    STA  jtmp + 2
    LDA  #6
    STA  jtmp + 1
.xloop
    LDA  jtmp + 1
    ASL  A
    ASL  A
    ASL  A
    ADC  xoffset
    SEC
    SBC  #3 + 3 * 8
    TAY
    LDX  jtmp + 2
    JSR  depth
    
    LDA  mathc + 1
    CLC
    ADC  #127
    LDY  offset
    STA  xlocs, Y
    LDY  #18
    LDA  jtmp
    CMP  #3
    BNE  nojmp
    LDA  jtmp + 1
    CMP  #1
    BNE  nojmp
    INY
.nojmp
    LDX  jtmp + 2
    JSR  depth
    
    LDA  mathc + 1
    ADC  #96
    LDY  offset
    STA  ylocs, Y
    INC  offset
    
    DEC  jtmp + 1
    BPL  xloop
    DEC  jtmp
    BPL  yloop
}

{
    LDA  #6
    STA  jtmp
    LDA  #0
    STA  offset
.xloop
    LDA  #5
    STA  jtmp + 1
    LDY  offset
    LDA  xlocs, Y
    STA  ocoord
    LDA  ylocs, Y
    STA  ocoord + 1
    INC  offset
.yloop
    LDA  ocoord
    STA  x0
    LDA  ocoord + 1
    STA  y0
    LDY  offset
    LDA  xlocs, Y
    STA  x1
    STA  ocoord
    LDA  ylocs, Y
    STA  y1
    STA  ocoord + 1
    INC  offset
    JSR  plot_line
    DEC  jtmp + 1
    BPL  yloop
    DEC  jtmp
    BPL  xloop
}

{
    LDA  #6
    STA  jtmp
    LDA  #0
    STA  offset
.xloop
    LDA  #5
    STA  jtmp + 1
    LDY  offset
    LDA  xlocs, Y
    STA  ocoord
    LDA  ylocs, Y
    STA  ocoord + 1
    LDA  offset
    CLC
    ADC  #7
    STA  offset
.yloop
    LDA  ocoord
    STA  x0
    LDA  ocoord + 1
    STA  y0
    LDY  offset
    LDA  xlocs, Y
    STA  x1
    STA  ocoord
    LDA  ylocs, Y
    STA  y1
    STA  ocoord + 1
    LDA  offset
    CLC
    ADC  #7
    STA  offset
    JSR  plot_line
    DEC  jtmp + 1
    BPL  yloop
    LDA  offset
    SEC
    SBC  #7 * 7 - 1
    STA  offset
    DEC  jtmp
    BPL  xloop
}

    JSR  swap_banks
    JSR  clear_bank
    INC  yoffset
    LDA  yoffset
    AND  #7
    STA  yoffset
    STA  xoffset
    JMP  endforever
    
.xlocs
    SKIP 7 * 7
.ylocs
    SKIP 7 * 7

.mark
    LDX  x1
    LDY  y1
    DEX
    DEX
    JSR  pixelp
    LDX  x1
    INX
    INX
    JSR  pixelp
    LDX  x1
    INY
    INY
    JSR  pixelp
    LDY  y1
    DEY
    DEY
    JMP  pixelp

INCLUDE  "screen.basm"
INCLUDE  "draw.basm"
INCLUDE  "math.basm"

.end

SAVE "LANDER", start, end
