; Head-up display. Well, not really, but I couldn't think of a better name for it!

; Update the fuel level
{
    LDA update_fuel
    BEQ no_fuelupdate
    LDA #LO(fuel_addr)
    STA write_fuel1 + 1
    STA write_fuel2 + 1
    LDA #HI(fuel_addr + screen1)
    STA write_fuel1 + 2
    LDA #HI(fuel_addr + screen2)
    STA write_fuel2 + 2
    LDX #4 * 8 + 5
    LDY #0
    STY update_fuel
    LDA current_fuel + 1
    ORA current_fuel
.fuel_loop
    CPY current_fuel + 1
    BEQ fuel_equal
    BCS fuel_over
    LDA #255
.write_fuel1
    STA &1234, X
.write_fuel2
    STA &1234, X
    INY
    DEX
    TXA
    AND #7
    CMP #7
    BNE fuel_loop
    DEX
    DEX
    BPL fuel_loop
    BMI no_fuelupdate
.fuel_over
    LDA #0
    BEQ write_fuel1
.fuel_equal
    LDA current_fuel
    LSR A
    EOR #7
    TAY
    LDA rightbits + 1, Y ; The byte after this is 0, by coincidence!
    LDY current_fuel + 1
    JMP write_fuel1
.no_fuelupdate
}

; Update the altitude
{
    LDA player_y
    LDY player_y + 1
    CMP last_altitude
    BNE alt_changed
    CPY last_altitude + 1
    BEQ alt_notchanged
.alt_changed
    STA last_altitude
    STA mathc
    STY last_altitude + 1
    STY mathc + 1
    LDX #LO(altitude_addr)
    LDY #HI(altitude_addr)
    JSR plot_3digits
.alt_notchanged
}

; Update the heading
{
    LDA yangle
    CMP last_heading
    BEQ heading_notchanged
    STA last_heading
    CLC
    ADC #64
    TAY
    LDX #180
    JSR usmulxy
    LDY #0
; mathc + 1 is 0..180
    LDA mathc + 1
    ASL mathc
    ROL A
    STA mathc
    BCC notover
    INY
.notover
    STY mathc + 1
; mathc is now 0..360
    LDX #LO(heading_addr)
    LDY #HI(heading_addr)
    JSR plot_3digits
.heading_notchanged
}

IF FALSE
; Draw the ship angle on top left
{
    LDA yangle
    JSR sincos
    TYA
    CLC
    ADC #40
    LSR A
    LSR A
    STA x0
    PHA
    TXA
    CLC
    ADC #88
    LSR A
    LSR A
    STA y0
    PHA
    LDA yangle
    CLC
    ADC #100
    JSR sincos
    TYA
    CLC
    ADC #40
    LSR A
    LSR A
    STA x1
    TXA
    CLC
    ADC #88
    LSR A
    LSR A
    STA y1
    JSR plot_line

    PLA
    STA y0
    PLA
    STA x0
    LDA yangle
    SEC
    SBC #100
    JSR sincos
    TYA
    CLC
    ADC #40
    LSR A
    LSR A
    STA x1
    TXA
    CLC
    ADC #88
    LSR A
    LSR A
    STA y1
    JSR plot_line
}
ENDIF
