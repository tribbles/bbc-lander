particle_count = 16

.particle_xl
    SKIP particle_count
.particle_xh
    SKIP particle_count
.particle_dx
    SKIP particle_count
.particle_yl
    SKIP particle_count
.particle_yh
    SKIP particle_count
.particle_dy
    SKIP particle_count
.particle_zl
    SKIP particle_count
.particle_zh
    SKIP particle_count
.particle_dz
    SKIP particle_count
.particle_age
    SKIP particle_count

MACRO MOVPART dirl, dirh, thrust, delta, negthrust
{
    LDY #0
    LDA delta, X
    ASL A
    ASL A
IF thrust != 0
IF negthrust
    SEC
    SBC thrust
ELSE
    CLC
    ADC thrust 
ENDIF
ENDIF
    BEQ nextmove    ; Delta is zero, so no movement required
    BPL positive
    DEY
.positive
    STY jtmp + 2
    ASL A
    ROL jtmp + 2
    ASL A
    ROL jtmp + 2
    CLC
    ADC dirl, X
    STA dirl, X
    LDA jtmp + 2
    ADC dirh, X
    STA dirh, X
.nextmove
}
ENDMACRO

.create_particle
{
    LDX current_particle
    DEX
    BPL no_wrap
    LDX #particle_count - 1
.no_wrap
    STX current_particle
    LDA #16
    STA particle_age, X
    LDA #0
    STA particle_xh, X
    STA particle_yh, X
    STA particle_zh, X
    STA particle_xl, X
    STA particle_yl, X
    STA particle_zl, X
    LDA particle_xdir
    STA particle_dx, X
    LDA particle_ydir
    STA particle_dy, X
    LDA particle_zdir
    STA particle_dz, X
    RTS
}

.move_particles
{
    LDX #particle_count - 1
.particle_loop
    LDA particle_age, X
    BEQ particle_old
    DEC particle_age, X
    MOVPART particle_xl, particle_xh, xthrust, particle_dx, FALSE
    MOVPART particle_yl, particle_yh, 0, particle_dy, FALSE
    MOVPART particle_zl, particle_zh, zthrust, particle_dz, FALSE
.particle_old
    DEX
    BMI particles_done
    JMP particle_loop
.particles_done
    RTS
}

.plot_particles
{
    LDX #particle_count - 1
.particle_loop
    LDA particle_age, X
    BEQ particle_old
    ;LDA #'a'
    ;JSR oswrch
    STX jtmp
    LDA particle_xh, X
    TAY
    LDA particle_zh, X
    CLC
    ADC #32
    BMI particle_offscreen
    CMP #63
    BCS particle_offscreen

    STA jtmp + 1
    TAX
    JSR depthX
    LDA mathc + 1
    CLC
    ADC #127
    TAX
    LDA mathc + 2
    ADC #0
    BNE particle_offscreen
    STX x0
    ;LDA #'b'
    ;JSR oswrch

    LDX jtmp
    LDA particle_yh, X

    CLC
    ADC land_height
    BCS particle_offscreen
    BMI particle_offscreen
    TAY
    ;LDA #'c'
    ;JSR oswrch

    LDX jtmp + 1
    JSR depthY

    LDA mathc + 1
    CLC
    ADC #40        ; This is the height
    TAY
    LDA mathc + 2
    ADC #0
    BNE particle_offscreen
    ;LDA #'d'
    ;JSR oswrch
    LDX x0
    JSR pixel
    LDX jtmp
    BPL particle_old

.particle_offscreen
    LDX jtmp
    LDA #0
    STA particle_age, X
.particle_old
    DEX
    BPL particle_loop
    RTS
}
